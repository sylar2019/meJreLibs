ext {
    isLib = this.&isLib
    isApp = this.&isApp
    isJavaProject = this.&isJavaProject
    isParentEquals = this.&isParentEquals
    isProjectEndsWith = this.&isProjectEndsWith
    isProjectEquals = this.&isProjectEquals
}

def isLib() {
    Set result = isJavaProject()
    result.removeAll(isApp())
    return result
}

def isApp() {
    Set result = isJavaProject()

    Set set = isParentEquals("app")
    set.addAll(isProjectEquals("app"))
    set.addAll(isProjectEndsWith("app"))

    result.retainAll(set)
    return result
}

def isParentEquals(parentName) {
    return subprojects.findAll {
        it.parent != null && it.parent.name == parentName
    }
}

def isProjectEndsWith(suffix) {
    return subprojects.findAll {
        it.name.endsWith(suffix)
    }
}

def isProjectEquals(projectName) {
    return subprojects.findAll {
        it.name == projectName
    }
}

def isJavaProject() {
    return subprojects.findAll {
        it.childProjects.size() == 0 && new File(it.projectDir, "src").exists()
    }
}
